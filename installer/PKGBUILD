# Maintainer: AnonymerNiklasistanonym <niklas.mikeler@gmail.com>

# Custom variables:
# The name of the application that is built by this package
_applicationname="moonpiebot"
# The name of the cloned git directory of the application
_gitname="$_applicationname.git"
# The install directory of the application
_installdir="/opt/$_applicationname"
# The install directory for the global binaries
_installdirbin="/usr/bin"
# The install directory for the .desktop files
_installdirdesktop="/usr/share/applications"
# The install directory for the man pages
_installdirman="/usr/share/man/man1"
# The install directory for the man pages
_defaultconfigdir="$HOME/.local/share/$_applicationname"

pkgname="$_applicationname"
pkgver="1.0.28b"
pkgrel="1"
pkgdesc="A custom Twitch chat bot"
arch=("x86_64")
url="https://github.com/AnonymerNiklasistanonym/MoonpieBot"
license=("MIT")
# Node.js runtime is required
depends=("nodejs")
# Node.js and npm is required to build the program
# Pandoc is required to create the man page
makedepends=("nodejs" "npm" "pandoc")
provides=("$pkgname")
conflicts=("$_applicationname" "$_applicationname-bin" "$_applicationname-git")
source=("$_gitname::git+$url#tag=v$pkgver")
sha1sums=("SKIP")

# Create all files necessary for installation
build() {
  # Go to the GIT directory in the source directory
  cd "$srcdir/$_gitname"

  # Install build and runtime dependencies
  npm install

  # Install uuid dependency because it throws errors on some systems
  npm install uuid

  # Compile TypeScript application to JavaScript
  npm run build

  # Remove build dependencies
  npm prune --production

  # Create manpage
  pandoc "installer/man.md" -s -t man -o "$srcdir/$_applicationname.1"

  # Create application icon
  cp "res/icons/$_applicationname.svg" "$srcdir/$_applicationname.svg"
}

# Define where files should be installed
package() {
  # Go to the GIT directory in the source directory
  cd "$srcdir/$_gitname"

  # Create a directory in /opt and copy the files necessary for the runtime there
  install -Dd "$pkgdir$_installdir"
  find node_modules -not -path "*/__pycache__/*" -exec install -D {} "$pkgdir$_installdir/"{} \;
  find dist -exec install -D {} "$pkgdir$_installdir/"{} \;
  install -D package.json "$pkgdir$_installdir"

  # Install the application icon
  install -Dm 644 "$srcdir/$_applicationname.svg" "$pkgdir$_installdir"

  # Create and install a global script to start the application
  echo -e "\
#!/usr/bin/env bash\n\
node \"$_installdir\" --config-dir \"$_defaultconfigdir\" \$@\n" > "$_applicationname"
  install -Dd "$pkgdir$_installdirbin"
  install -Dm 755 "$_applicationname" "$pkgdir$_installdirbin"

  # Create and install .desktop entry
  echo -e "\
[Desktop Entry]\n\
Version=1.0\n\
Type=Application\n\
Terminal=true\n\
Exec=$_applicationname; read -n1 -p \"Press any key to exit.\"\n\
Name=$_applicationname\n\
Comment=$pkgdesc\n\
Icon=$_installdir/$_applicationname.svg\n" > "$_applicationname.desktop"
  install -Dd "$pkgdir$_installdirdesktop"
  install -Dm 644 "$_applicationname.desktop" "$pkgdir$_installdirdesktop"

  # Install manpage
  install -Dm 644 "$srcdir/$_applicationname.1" "$pkgdir$_installdirman/$_applicationname.1"
}
